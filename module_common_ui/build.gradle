apply from: "${rootProject.rootDir}/config.gradle"
if (Boolean.valueOf(rootProject.ext.moduleCommonUi)) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply plugin: 'me.tatarka.retrolambda' //lambda配置
}
apply plugin: 'com.jakewharton.butterknife'

android {
    project.ext.setDefaultConfig project

    defaultConfig {

        //Arouter路由配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /*java插件引入了一个概念叫做SourceSets，通过修改SourceSets中的属性，可以指定哪些源文件
   （或文件夹下的源文件）要被编译，哪些源文件要被排除。*/
    sourceSets {
        main {
            if (rootProject.ext.moduleCommonUi) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //排除java/debug文件夹下的所有文件
                    exclude '*module'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    //Arouter路由
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.arouterCompilerVersion"
    //黄油刀
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeVersion"

    //6.0权限
    implementation 'pub.devrel:easypermissions:1.2.0'

    //公用依赖包
    api project(':projectCore')


}
